#ifdef __MSX__
#include "asm.h"

void AsmCall(uint address, Z80_registers* regs, register_usage inRegistersDetail, register_usage outRegistersDetail) __naked
{
#asm
    push ix
    ld ix,#4
    add ix,sp

    ld l,(ix)
    ld h,1(ix)
    ld e,2(ix) 
    ld d,3(ix)
    push de
    ld a,5(ix) 
    push af
    ld a,4(ix)

    push de
    pop ix

    ld de,#CONT
    push de
    push hl

    or a
    ret z   

    exx
    ld l,(ix)
    ld h,1(ix)
    dec a
    jr z,ASMRUT_DOAF
    exx

    ld c,2(ix)
    ld b,3(ix)
    ld e,4(ix)
    ld d,5(ix)
    ld l,6(ix)
    ld h,7(ix)
    dec a
    exx
    jr z,ASMRUT_DOAF

    ld c,8(ix)
    ld b,9(ix)
    ld e,10(ix)
    ld d,11(ix)
    push de
    push bc
    pop ix
    pop iy

ASMRUT_DOAF:
    push hl
    pop af
    exx

    ret
		   
CONT:

    ex af,af
    pop af
    ex (sp),ix 

    or a
    jr z,CALL_END

    exx
    ex af,af
    push af
    pop hl
    ld (ix),l
    ld 1(ix),h
    exx
    ex af,af
    dec a
    jr z,CALL_END

    ld 2(ix),c
    ld 3(ix),b
    ld 4(ix),e
    ld 5(ix),d
    ld 6(ix),l
    ld 7(ix),h
    dec	a
    jr z,CALL_END

    exx
    pop hl
    ld 8(ix),l
    ld 9(ix),h
    push iy
    pop hl
    ld 10(ix),l
    ld 11(ix),h
    exx

    ex af,af
    pop ix
    ret

CALL_END:
    ex af,af
    pop hl
    pop ix
    ret

#endasm
}
#endif /* __MSX__ */
